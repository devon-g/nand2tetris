// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // Check if the register should be changed
    Or(a=reset, b=load, out=or0);
    Or(a=or0, b=inc, out=change);

    // Handle flags
    Mux16(a=outinc, b=in, sel=load, out=outmuxload);
    Mux16(a=outmuxload, b=false, sel=reset, out=outmuxreset);

    // Handle output
    Register(in=outmuxreset, load=change, out=out, out=outr0);
    Inc16(in=outr0, out=outinc);
}
